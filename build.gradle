plugins {
    alias(libs.plugins.spotless)
}


allprojects {
    group = 'ru.funpay4j'
    version = '1.0.6'

    repositories {
        mavenCentral()
    }
}

def javaVersion = JavaVersion.current()

subprojects { subproject ->
    if (subproject.name != 'examples' && javaVersion == JavaVersion.VERSION_1_8) {
        apply plugin: 'com.diffplug.spotless'

        spotless {
            java {
                target("src/main/java/**/*.java", "src/test/java/**/*.java")

                googleJavaFormat().aosp()
                importOrder("java", "javax", "org", "com")
                removeUnusedImports()
                trimTrailingWhitespace()
                endWithNewline()
            }
        }
    }

    tasks.withType(Test).configureEach {
        systemProperty 'file.encoding', 'UTF-8'
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

}