buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:5.17.0'
    }
}

ext {
    jsoupVersion = '1.18.3'
    okhttpVersion = '4.12.0'
    gsonVersion = '2.12.1'
    lombokVersion = '1.18.36'
    junitVersion = '5.9.1'
}

allprojects {
    group = 'ru.funpay4j'
    version = '1.0.6'
}

def javaVersion = JavaVersion.current()

subprojects { subproject ->
    if (subproject.name != 'examples' && javaVersion.compareTo(JavaVersion.VERSION_1_8) == 0) {
        apply plugin: 'com.diffplug.spotless'

        spotless {
            java {
                target("src/main/java/**/*.java", "src/test/java/**/*.java")

                googleJavaFormat().aosp()
                importOrder("java", "javax", "org", "com")
                removeUnusedImports()
                trimTrailingWhitespace()
                endWithNewline()
            }
        }
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(Test).configureEach {
        systemProperty 'file.encoding', 'UTF-8'
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

}